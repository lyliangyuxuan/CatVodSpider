plugins {
    id 'com.android.application'
    id 'ru.cleverpumpkin.proguard-dictionaries-generator'
}

android {
    namespace 'com.github.catvod'

    compileSdk {
        version = release(36) {
            it.minorApiLevel = 1
        }
    }

    defaultConfig {
        applicationId "com.github.catvod.demo"
        minSdk 21
        targetSdk 36
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            proguardDictionaries {
                dictionaryNames = ["build/class-dictionary", "build/package-dictionary", "build/obfuscation-dictionary"]
                minLineLength 1
                maxLineLength 3
                linesCountInDictionary 100000
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    configurations.configureEach {
        resolutionStrategy {
            force 'com.squareup.okhttp3:okhttp:' + okhttpVersion
        }
    }
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:' + okhttpVersion
    implementation 'com.github.thegrizzlylabs:sardine-android:0.9'
    implementation 'wang.harlon.quickjs:wrapper-android:3.2.3'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.hierynomus:smbj:0.14.0'
    implementation 'com.orhanobut:logger:2.2.0'
    implementation 'org.jsoup:jsoup:1.21.2'
}
tasks.register('buildJar', Jar) {
    archiveBaseName = "CatVodSpider"
    archiveVersion = "1.0.0"
    archiveClassifier = ""
    destinationDirectory = file("$buildDir/jar")

    from android.sourceSets.main.java.srcDirs

    exclude("**/R.class")
    exclude("**/BuildConfig.class")
}
// ===============================
// Spider JAR (output to /jar)
// aligned with TVBox-jar project
// ===============================

def jarOutDir = file("${rootProject.projectDir}/jar")

tasks.register('genSpiderJar', Jar) {
    group = "spider"
    description = "Generate spider jar to /jar directory"

    // 固定 jar 名称
    archiveFileName.set("custom_spider.jar")

    // 固定输出目录
    destinationDirectory.set(jarOutDir)

    // 使用 release 编译后的 class
    from("$buildDir/intermediates/javac/release/classes")

    // 确保先编译
    dependsOn("assembleRelease")
}

// 生成 md5（和第二个成功项目一致）
tasks.register('genSpiderMd5') {
    group = "spider"
    description = "Generate md5 for spider jar"

    dependsOn("genSpiderJar")

    doLast {
        def jarFile = file("${rootProject.projectDir}/jar/custom_spider.jar")
        def md5 = java.security.MessageDigest
                .getInstance("MD5")
                .digest(jarFile.bytes)
                .encodeHex()
                .toString()

        file("${jarFile.absolutePath}.md5").text = md5
    }
}
